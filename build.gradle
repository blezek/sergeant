apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'application'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'edu.mayo.qia.qin.sergeant.Sergeant'


// Set our project variables
project.ext {
  dropwizardVersion = '1.0.5'
  springVersion = "3.2.2.RELEASE"

}

repositories {
  mavenCentral()
}

dependencies {
  // The production code uses the SLF4J logging API at compile time
  compile (
    'org.slf4j:slf4j-api:1.7.5',
    'io.dropwizard:dropwizard-core:' + dropwizardVersion,
    'io.dropwizard:dropwizard-forms:' + dropwizardVersion,
    'io.dropwizard:dropwizard-assets:' + dropwizardVersion,
    'org.glassfish.jersey.media:jersey-media-multipart:2.15',
    "org.zeroturnaround:zt-exec:1.5",
    "org.apache.commons:commons-exec:1.1",
    "org.quartz-scheduler:quartz:2.2.1",
    'us.levk:drmaa-gridengine:6.2u5',
    )

    testCompile "junit:junit:4.11"
  }

task collectJars(type: Copy){
  into "$buildDir/libs/lib"
  from configurations.compile
}

jar {
  manifest.attributes (
    'Main-Class': mainClassName,
    "Class-Path": configurations.compile.collect { "lib/" + it.name }.join(' ')
  )
}
jar.dependsOn collectJars

// Configure the run task to start the Dropwizard service
run {
  args 'server', 'sergeant.yml'
}

test {
  useJUnit();
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
}
